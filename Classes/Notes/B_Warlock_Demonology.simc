# Demonology Warlock
# https://github.com/simulationcraft/simc/
# December 22, 2020

# Changes:
# - Spell Lock.
# - Only recommend Demonbolt pre-combat if it is instant (or hardcast for bosses).
# - Power Siphon allowed pre-combat to proc instant Demonbolt.
# - Revise Implosion for legendary.
# - PvP talent interrupt.

# Executed before combat begins. Accepts non-harmful actions only.
actions.precombat+=/fel_domination,if=time>0&!pet.alive
actions.precombat+=/summon_pet
actions.precombat+=/power_siphon,if=buff.wild_imps.stack>1&buff.demonic_core.stack<=2
actions.precombat+=/demonbolt,if=action.demonbolt.cast=0&soul_shards<4
# actions.precombat+=/variable,name=tyrant_ready,value=cooldown.summon_demonic_tyrant.remains<5&(!talent.doom.enabled|debuff.doom.up)&(buff.demonic_strength.up|!talent.demonic_strength.enabled|talent.demonic_consumption.enabled)&(!talent.nether_portal.enabled|buff.nether_portal.up|cooldown.nether_portal.remains>15)&(!talent.grimoire_felguard.enabled|cooldown.grimoire_felguard.remains>15)&(!talent.summon_vilefiend.enabled|cooldown.summon_vilefiend.remains>15)&(!talent.demonic_consumption.enabled|buff.wild_imps.up|soul_shard=5|buff.nether_portal.up&soul_shard>0)

# Executed every time the actor is available.
actions=pet_attack,use_while_casting=1,if=buff.casting.up&buff.casting.remains>=0.5&(solo|group|target.affecting_combat)
actions+=/variable,name=significant_fight,value=false
# actions+=/variable,name=significant_fight,value=true,if=solo&time>=4&target.time_to_die>=14&target.health_pct<=90
actions+=/variable,name=significant_fight,value=true,if=in_pvp&target.is_player&target.health_pct>=60
actions+=/variable,name=significant_fight,value=true,if=(raid|group)&target.exists&(boss|target.is_boss)
actions+=/spell_lock
# this is the pvp talent interrupt
actions+=/call_felhunter
actions+=/axe_toss,use_while_casting=1
actions+=/devour_magic
actions+=/call_action_list,name=off_gcd
actions+=/mortal_coil,if=(health.pct<=80&solo)|health.pct<=60
actions+=/use_item,name=inscrutable_quantum_device,if=buff.tyrant.up|(boss&fight_remains<cooldown.summon_demonic_tyrant.remains+30)
actions+=/run_action_list,name=tyrant_prep,strict=1,if=!moving&ttds_after_30&cooldown.summon_demonic_tyrant.remains<4&!tyrant_ready
actions+=/run_action_list,name=summon_tyrant,strict=1,if=!moving&ttds_after_30&tyrant_ready
actions+=/summon_vilefiend,if=cooldown.summon_demonic_tyrant.remains>40|fight_remains<cooldown.summon_demonic_tyrant.remains+25
actions+=/call_dreadstalkers
actions+=/doom,if=refreshable
actions+=/demonic_strength
actions+=/bilescourge_bombers
actions+=/implosion,if=active_enemies>1&!talent.sacrificed_souls.enabled&buff.wild_imps.stack>=settings.implosion_imps&buff.tyrant.down&cooldown.summon_demonic_tyrant.remains>5&(buff.implosive_potential.down|!runeforge.implosive_potential.enabled)
actions+=/implosion,if=active_enemies>2&buff.wild_imps.stack>=settings.implosion_imps&buff.tyrant.down&(buff.implosive_potential.down|!runeforge.implosive_potential.enabled)
actions+=/hand_of_guldan,if=soul_shard=5|buff.nether_portal.up
actions+=/hand_of_guldan,if=soul_shard>=3&cooldown.summon_demonic_tyrant.remains>20&(cooldown.summon_vilefiend.remains>5|!talent.summon_vilefiend.enabled)&cooldown.call_dreadstalkers.remains>2
actions+=/call_action_list,name=covenant,strict=1,if=(covenant.necrolord|covenant.night_fae)&!talent.nether_portal.enabled
actions+=/demonbolt,if=buff.demonic_core.react&soul_shard<4
actions+=/grimoire_felguard,if=ttds_after_15&(cooldown.summon_demonic_tyrant.remains+cooldown.summon_demonic_tyrant.duration>fight_remains|fight_remains<cooldown.summon_demonic_tyrant.remains+15)
# actions+=/use_items
actions+=/use_item,name=darkmoon_deck_putrescence,if=ttds_after_8>1|(boss&ttds_after_8>0)|(target.is_boss&target.time_to_die<30)
actions+=/power_siphon,if=buff.wild_imps.stack>1&buff.demonic_core.stack<3
actions+=/soul_strike
actions+=/call_action_list,name=covenant
actions+=/shadow_bolt
actions+=/pet_attack,if=moving&(solo|target.affecting_combat)

actions.covenant=impending_catastrophe,if=!talent.sacrificed_souls.enabled|active_enemies>1
actions.covenant+=/scouring_tithe,if=talent.sacrificed_souls.enabled&active_enemies=1
actions.covenant+=/scouring_tithe,if=!talent.sacrificed_souls.enabled&active_enemies<4
actions.covenant+=/soul_rot,if=ttds_after_8>=3|ttds_after_6>=4|target.is_boss
actions.covenant+=/decimating_bolt

actions.off_gcd=berserking,if=pet.demonic_tyrant.active
actions.off_gcd+=/potion,if=variable.significant_fight&(buff.berserking.up|pet.demonic_tyrant.active&!race.troll)
actions.off_gcd+=/blood_fury,if=pet.demonic_tyrant.active
actions.off_gcd+=/fireblood,if=pet.demonic_tyrant.active

actions.summon_tyrant=hand_of_guldan,if=soul_shard=5
actions.summon_tyrant+=/demonbolt,if=buff.demonic_core.up&(talent.demonic_consumption.enabled|buff.nether_portal.down),line_cd=20
actions.summon_tyrant+=/shadow_bolt,if=buff.wild_imps.stack+incoming_imps<4&(talent.demonic_consumption.enabled|buff.nether_portal.down),line_cd=20
actions.summon_tyrant+=/grimoire_felguard
actions.summon_tyrant+=/summon_vilefiend
actions.summon_tyrant+=/call_dreadstalkers
actions.summon_tyrant+=/hand_of_guldan
actions.summon_tyrant+=/demonbolt,if=buff.demonic_core.up&buff.nether_portal.up&((buff.vilefiend.remains>5|!talent.summon_vilefiend.enabled)&(buff.grimoire_felguard.remains>5|buff.grimoire_felguard.down))
actions.summon_tyrant+=/shadow_bolt,if=buff.nether_portal.up&((buff.vilefiend.remains>5|!talent.summon_vilefiend.enabled)&(buff.grimoire_felguard.remains>5|buff.grimoire_felguard.down))
# actions.summon_tyrant+=/variable,name=tyrant_ready,value=!cooldown.summon_demonic_tyrant.ready
actions.summon_tyrant+=/summon_demonic_tyrant,if=!talent.demonic_consumption|buff.wild_imps.stack+imps_spawned_during.summon_demonic_tyrant>3
actions.summon_tyrant+=/shadow_bolt

actions.tyrant_prep=doom,line_cd=30
actions.tyrant_prep+=/demonic_strength,if=!talent.demonic_consumption.enabled
actions.tyrant_prep+=/nether_portal
actions.tyrant_prep+=/grimoire_felguard,if=soul_shard=5
actions.tyrant_prep+=/summon_vilefiend,if=soul_shard=4&(!talent.grimoire_felguard|cooldown.grimoire_felguard.remains)
actions.tyrant_prep+=/call_dreadstalkers,if=(!talent.grimoire_felguard|cooldown.grimoire_felguard.remains)
actions.tyrant_prep+=/demonbolt,if=buff.demonic_core.up&soul_shard<4&(talent.demonic_consumption.enabled|buff.nether_portal.down)
actions.tyrant_prep+=/shadow_bolt,if=soul_shard<5-4*buff.nether_portal.up
# actions.tyrant_prep+=/variable,name=tyrant_ready,value=1
actions.tyrant_prep+=/hand_of_guldan