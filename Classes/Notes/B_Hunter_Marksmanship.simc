# Marksmanship Hunter
# December 20, 2020

actions.precombat+=/bottled_flayedwing_toxin
actions.precombat+=/tar_trap,if=runeforge.soulforge_embers
actions.precombat+=/misdirection,if=group_focus_exists&cooldown.misdirection.ready&buff.misdirection.down
actions.precombat+=/double_tap,precast_time=10,if=target.exists&target.within40&(active_enemies>1|!covenant.kyrian&!talent.volley)
actions.precombat+=/aimed_shot,if=active_enemies<3&(!covenant.kyrian&!talent.volley|active_enemies<2)
actions.precombat+=/steady_shot,if=active_enemies>2|(covenant.kyrian|talent.volley)&active_enemies=2

actions.vars=variable,name=significant_fight,value=false
# actions.vars+=/variable,name=significant_fight,value=true,if=solo&time>=4&target.time_to_die>=14&target.health_pct<=90
# actions.vars+=/variable,name=significant_fight,value=true,if=in_pvp&target.is_player&target.health_pct>=60
actions.vars+=/variable,name=significant_fight,value=true,if=(raid|group)&target.exists&target.is_boss
actions.vars+=/variable,name=significant_fight,value=true,if=group&!raid&time>=2&target.health_max>=100000&target.health_pct<=95&target.time_to_die>=20

actions.defense=survival_of_the_fittest,if=incoming_damage_6s>=0.33*health.max
actions.defense+=/exhilaration,if=health.pct<=60
actions.defense+=/aspect_of_the_turtle,if=health.pct<=15

actions+=/call_action_list,name=vars
actions+=/counter_shot,use_while_casting=1
actions+=/scatter_shot,use_while_casting=1,if=cooldown.counter_shot.remains>1
# TODO:  This will need support to allow Counter Shot when target is not casting.
actions+=/counter_shot,line_cd=30,if=runeforge.sephuzs_proclamation|soulbind.niyas_tools_poison|(conduit.reversal_of_fortune&!runeforge.sephuzs_proclamation)
actions+=/call_action_list,name=defense
actions+=/sniper_shot,if=target.outside45
actions+=/tranquilizing_shot
actions+=/bursting_shot,if=in_pvp&target.within7
actions+=/viper_sting,if=target.is_healer
# Use this on cooldown if it's the only on-use. If you have another on-use with a longer cooldown, stack that with Trueshot when their cooldowns conflict, so wait until it's on cooldown to use this, otherwise continue to use on cooldown.
actions+=/use_item,name=dreadfire_vessel
# If two on-uses are ready and competing for a Trueshot sync, assume the longer cd has a stronger effect and prefer that unless it's already on cooldown, otherwise use the one that's off cooldown. If a trinket is ready and another stronger (assumed from longer cooldown) on-use will also be ready by the time Trueshot comes back off cooldown, then use it if the shared 20 second cd it triggers won't interfere with the upcoming Trueshot sync'd on-use. If a trinket could be used and still come back off cooldown for a future Trueshot, it's safe to use, preferring the longest cooldown if two are ready. If the fight is going to end before your next Trueshot, just start using trinkets to make sure they get used.
# actions+=/use_items,slots=trinket1,if=buff.trueshot.up&(trinket.1.cooldown.duration>=trinket.2.cooldown.duration|trinket.2.cooldown.remains)|buff.trueshot.down&cooldown.trueshot.remains>20&trinket.2.cooldown.duration>=trinket.1.cooldown.duration&trinket.2.cooldown.remains-5<cooldown.trueshot.remains&!trinket.2.is.dreadfire_vessel|(trinket.1.cooldown.duration-5<cooldown.trueshot.remains&(trinket.1.cooldown.duration>=trinket.2.cooldown.duration|trinket.2.cooldown.remains))|target.time_to_die<cooldown.trueshot.remains
# actions+=/use_items,slots=trinket2,if=buff.trueshot.up&(trinket.2.cooldown.duration>=trinket.1.cooldown.duration|trinket.1.cooldown.remains)|buff.trueshot.down&cooldown.trueshot.remains>20&trinket.1.cooldown.duration>=trinket.2.cooldown.duration&trinket.1.cooldown.remains-5<cooldown.trueshot.remains&!trinket.1.is.dreadfire_vessel|(trinket.2.cooldown.duration-5<cooldown.trueshot.remains&(trinket.2.cooldown.duration>=trinket.1.cooldown.duration|trinket.1.cooldown.remains))|target.time_to_die<cooldown.trueshot.remains
actions+=/use_items,if=buff.trueshot.up|buff.trueshot.down&cooldown.trueshot.remains>20|fight_remains<cooldown.trueshot.remains
actions+=/call_action_list,name=cds
actions+=/concussive_shot,if=!raid&refreshable&target.on_me&target.moving
actions+=/concussive_shot,if=refreshable&in_pvp&target.is_player&target.moving
actions+=/hunters_mark,if=in_pvp&target.is_player&(target.class='rogue'|target.class='druid'|target.class='hunter')
actions+=/call_action_list,name=st,if=active_enemies<3
actions+=/call_action_list,name=trickshots,if=active_enemies>2

actions.cds=berserking,if=buff.trueshot.up|fight_remains<13
actions.cds+=/blood_fury,if=buff.trueshot.up|fight_remains<16
actions.cds+=/ancestral_call,if=buff.trueshot.up|fight_remains<16
actions.cds+=/fireblood,if=buff.trueshot.up|fight_remains<9
actions.cds+=/lights_judgment,if=buff.trueshot.down
actions.cds+=/bag_of_tricks,if=buff.trueshot.down
actions.cds+=/potion,if=variable.significant_fight&(buff.bloodlust.up|buff.trueshot.up)

actions.st=steady_shot,if=talent.steady_focus&(prev_gcd.1.steady_shot&buff.steady_focus.remains<5|buff.steady_focus.down)
actions.st+=/kill_shot
actions.st+=/double_tap,if=target.exists&target.within40&(covenant.kyrian&cooldown.resonating_arrow.remains<gcd|!covenant.kyrian&!covenant.night_fae|covenant.night_fae&(cooldown.wild_spirits.remains<gcd|cooldown.trueshot.remains>55)|target.time_to_die<15)
actions.st+=/flare,line_cd=25,if=tar_trap.up&runeforge.soulforge_embers
actions.st+=/tar_trap,if=runeforge.soulforge_embers&tar_trap.remains<gcd&cooldown.flare.remains<gcd
actions.st+=/explosive_shot,if=target.time_to_die>=4
actions.st+=/wild_spirits,if=target.is_mouseover&variable.significant_fight&solo
actions.st+=/flayed_shot
actions.st+=/death_chakram,if=focus+cast_regen<focus.max
actions.st+=/a_murder_of_crows
actions.st+=/resonating_arrow,if=target.is_mouseover
actions.st+=/volley,if=fight_remains>=6&target.is_mouseover&(buff.precise_shots.down|!talent.chimaera_shot|active_enemies<2)
actions.st+=/trueshot,if=fight_remains>=15&target.within40&target.exists&(buff.precise_shots.down|buff.resonating_arrow.up|buff.wild_spirits.up|buff.volley.up&active_enemies>1)
# TODO: Review action.serpent_sting.in_flight_to_target
actions.st+=/aimed_shot,cycle_targets=1,if=buff.precise_shots.down|(buff.trueshot.up|full_recharge_time<gcd+cast_time)&(!talent.chimaera_shot|active_enemies<2)|buff.trick_shots.remains>execute_time&active_enemies>1
actions.st+=/rapid_fire,if=focus+cast_regen<focus.max&(buff.trueshot.down|!runeforge.eagletalons_true_focus)&(buff.double_tap.down|talent.streamline)
actions.st+=/chimaera_shot,if=buff.precise_shots.up|focus>cost+action.aimed_shot.cost
actions.st+=/arcane_shot,if=buff.precise_shots.up|focus>cost+action.aimed_shot.cost
actions.st+=/serpent_sting,cycle_targets=1,if=refreshable&target.time_to_die>duration
actions.st+=/barrage,if=active_enemies>1
actions.st+=/rapid_fire,if=focus+cast_regen<focus.max&(buff.double_tap.down|talent.streamline)
actions.st+=/steady_shot

actions.trickshots=steady_shot,if=talent.steady_focus&in_flight&buff.steady_focus.remains<5
actions.trickshots+=/double_tap,if=target.exists&target.within40&(covenant.kyrian&cooldown.resonating_arrow.remains<gcd|!covenant.kyrian&!covenant.night_fae|covenant.night_fae&(cooldown.wild_spirits.remains<gcd|cooldown.trueshot.remains>55)|target.time_to_die<10)
actions.trickshots+=/tar_trap,if=runeforge.soulforge_embers&tar_trap.remains<gcd&cooldown.flare.remains<gcd
actions.trickshots+=/flare,line_cd=25,if=tar_trap.up&runeforge.soulforge_embers
actions.trickshots+=/explosive_shot,if=target.time_to_die>=4
actions.trickshots+=/wild_spirits,if=target.is_mouseover&variable.significant_fight&solo
actions.trickshots+=/resonating_arrow
actions.trickshots+=/volley,if=fight_remains>=6&target.is_mouseover
actions.trickshots+=/barrage
actions.trickshots+=/trueshot,if=fight_remains>=15&target.within40&target.exists
actions.trickshots+=/rapid_fire,if=buff.trick_shots.remains>=execute_time&runeforge.surging_shots&buff.double_tap.down
actions.trickshots+=/aimed_shot,cycle_targets=1,if=buff.trick_shots.remains>=execute_time&(buff.precise_shots.down|full_recharge_time<cast_time+gcd|buff.trueshot.up)
actions.trickshots+=/death_chakram,if=focus+cast_regen<focus.max
actions.trickshots+=/rapid_fire,if=buff.trick_shots.remains>=execute_time
actions.trickshots+=/multishot,if=buff.trick_shots.down|buff.precise_shots.up&focus>cost+action.aimed_shot.cost&(!talent.chimaera_shot|active_enemies>3)
actions.trickshots+=/chimaera_shot,if=buff.precise_shots.up&focus>cost+action.aimed_shot.cost&active_enemies<4
actions.trickshots+=/kill_shot,if=buff.dead_eye.down
actions.trickshots+=/a_murder_of_crows
actions.trickshots+=/flayed_shot
actions.trickshots+=/serpent_sting,cycle_targets=1,if=refreshable
actions.trickshots+=/multishot,if=focus>cost+action.aimed_shot.cost
actions.trickshots+=/steady_shot
