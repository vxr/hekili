# Death Knight Unholy
# December 16, 2020

# Adjustments to DnD/Defile/Deaths Due to only count targets who are expected to live long enough to matter (aoe_setup #1).

# actions.precombat+=/potion
actions.precombat+=/use_item,name=azsharas_font_of_power
actions.precombat+=/raise_dead

actions.dead_end=variable,name=dead_end,value=1

# Anti-CC
actions.anti_cc=use_item,name=sinful_gladiators_medallion,if=is_cced
actions.anti_cc+=/icebound_fortitude,interrupt=1,if=is_cced
actions.anti_cc+=/lichborne,if=is_cced&cooldown.icebound_fortitude.remains&buff.icebound_fortitude.down
# Anti-Slow
actions.anti_cc+=/wraith_walk,if=(is_rooted|is_snared)&target.exists&!target.in_range
actions.anti_cc+=/run_action_list,name=dead_end,if=buff.wraith_walk.up&target.exists&!target.in_range
actions.anti_cc+=/deaths_advance,if=(is_rooted|is_snared)&target.exists&!target.in_range&cooldown.wraith_walk.remains&buff.wraith_walk.down

# Sustain
actions.sustain_solo=death_strike,if=(health.pct<=80&incoming_damage_5s>=0.2*health.max)|health.pct<=50
actions.sustain_solo+=/death_strike,if=(health.pct<=80&buff.dark_succor.up)|(health.pct<=95&buff.dark_succor.remains<=3)
actions.sustain_solo+=/antimagic_shell,if=runic_power.deficit>=35&incoming_magic_5s>=2000
actions.sustain_solo+=/icebound_fortitude,if=health.pct<=45&incoming_damage_5s>=0.2*health.max
actions.sustain_solo+=/lichborne,if=target.is_enemy&target.within7&health.pct<=60&target.time_to_die>=8
actions.sustain_solo+=/antimagic_zone,if=health.pct<=80&incoming_magic_5s>0.25*health.max&target.in_range
actions.sustain_solo+=/healthstone,if=combat&health.pct<=40&incoming_damage_4s>0.25*health.max

# Sustain Group
actions.sustain_group=antimagic_shell,if=runic_power.deficit>=35&incoming_magic_5s>=2000
actions.sustain_group+=/icebound_fortitude,if=health.pct<=45&incoming_damage_5s>=0.2*health.max
actions.sustain_group+=/lichborne,if=target.is_enemy&target.within7&health.pct<=60&target.time_to_die>=8
actions.sustain_group+=/healthstone,if=combat&health.pct<=40&incoming_damage_4s>0.25*health.max

# Sustain Raid
actions.sustain_raid=antimagic_shell,if=runic_power.deficit>=35&incoming_magic_5s>=2000
actions.sustain_raid+=/icebound_fortitude,if=health.pct<=45&incoming_damage_5s>=0.2*health.max
actions.sustain_raid+=/lichborne,if=target.is_enemy&target.within7&health.pct<=60&target.time_to_die>=8
actions.sustain_raid+=/healthstone,if=combat&health.pct<=40&incoming_damage_4s>0.25*health.max

# PvP
actions.pvp=death_grip,if=in_pvp&target.is_player&target.outside12
actions.pvp+=/chains_of_ice,if=in_pvp&target.is_player&target.outside12&cooldown.death_grip.remains>1&debuff.chains_of_ice.down

# Tries to make sure we always have something to do
actions.low_priority=festering_strike,rune.pct=100
actions.low_priority+=/wound_spender,if=debuff.festering_wound.stack>=1

# Executed every time the actor is available.
actions=mind_freeze
# Variables
actions+=/variable,name=pooling_for_gargoyle,value=cooldown.summon_gargoyle.remains<5&talent.summon_gargoyle
actions+=/variable,name=st_planning,value=active_enemies=1&(!raid_event.adds.exists|raid_event.adds.in>15)
actions+=/variable,name=significant_fight,value=false
actions+=/variable,name=significant_fight,value=true,if=solo&time>=4&target.time_to_die>=14&target.health_pct<=90
actions+=/variable,name=significant_fight,value=true,if=in_pvp&target.is_player&target.health_pct>=60
actions+=/variable,name=significant_fight,value=true,if=(raid|group)&target.exists&target.is_boss
actions+=/variable,name=cooldown_army_remains,value=cooldown.army_of_the_dead.remains,if=variable.significant_fight
actions+=/variable,name=cooldown_army_remains,value=400,if=!variable.significant_fight
actions+=/call_action_list,name=anti_cc
actions+=/call_action_list,name=sustain_solo,if=solo
actions+=/call_action_list,name=sustain_raid,if=raid
actions+=/call_action_list,name=sustain_group,if=group
actions+=/call_action_list,name=pvp,if=in_pvp
# Racials
actions+=/arcane_torrent,if=runic_power.deficit>65&(pet.gargoyle.active|!talent.summon_gargoyle.enabled)&rune.deficit>=5
# Other
actions+=/blood_fury,if=pet.gargoyle.active|buff.unholy_assault.up|talent.army_of_the_damned&pet.apoc_ghoul.active&(pet.army_ghoul.active|variable.cooldown_army_remains>cooldown.blood_fury.duration%3)|fight_remains<=buff.blood_fury.duration
actions+=/berserking,if=pet.gargoyle.active|buff.unholy_assault.up|talent.army_of_the_damned&pet.apoc_ghoul.active&(pet.army_ghoul.active|variable.cooldown_army_remains>cooldown.berserking.duration%3)|fight_remains<=buff.berserking.duration
actions+=/lights_judgment,if=buff.unholy_strength.up
# Ancestral Call can trigger 4 potential buffs, each lasting 15 seconds. Utilized hard coded time as a trigger to keep it readable.
actions+=/ancestral_call,if=pet.gargoyle.active|buff.unholy_assault.up|talent.army_of_the_damned&pet.apoc_ghoul.active&(pet.army_ghoul.active|variable.cooldown_army_remains>cooldown.ancestral_call.duration%3)|fight_remains<=15
actions+=/arcane_pulse,if=active_enemies>=2|(rune.deficit>=5&runic_power.deficit>=60)
actions+=/fireblood,if=pet.gargoyle.active|buff.unholy_assault.up|talent.army_of_the_damned&pet.apoc_ghoul.active&(pet.army_ghoul.active|variable.cooldown_army_remains>cooldown.fireblood.duration%3)|fight_remains<=buff.fireblood.duration
actions+=/bag_of_tricks,if=buff.unholy_strength.up&active_enemies=1
# Maintaining Virulent Plague is a priority
actions+=/outbreak,if=dot.virulent_plague.refreshable&!talent.unholy_blight&!raid_event.adds.exists
actions+=/outbreak,if=dot.virulent_plague.refreshable&active_enemies>=2&(!talent.unholy_blight|talent.unholy_blight&cooldown.unholy_blight.remains)
actions+=/outbreak,if=runeforge.superstrain&(dot.frost_fever.refreshable|dot.blood_plague.refreshable)
# Action Lists
actions+=/call_action_list,name=covenants
actions+=/call_action_list,name=cooldowns
actions+=/run_action_list,name=aoe_setup,if=active_enemies>=2&(cooldown.death_and_decay.remains<10&!talent.defile|cooldown.defile.remains<10&talent.defile)&!death_and_decay.ticking
actions+=/run_action_list,name=aoe_burst,if=active_enemies>=2&death_and_decay.ticking
actions+=/run_action_list,name=generic_aoe,if=active_enemies>=2&(!death_and_decay.ticking&(cooldown.death_and_decay.remains>10&!talent.defile|cooldown.defile.remains>10&talent.defile))
actions+=/call_action_list,name=generic,if=active_enemies=1
actions+=/call_action_list,name=low_priority

# AoE Burst
actions.aoe_burst=death_coil,if=buff.dark_transformation.up&runeforge.deadliest_coil&active_enemies<=3
actions.aoe_burst+=/epidemic,if=runic_power.deficit<(10+death_knight.fwounded_targets*3)&death_knight.fwounded_targets<6&!variable.pooling_for_gargoyle|buff.swarming_mist.up
actions.aoe_burst+=/epidemic,if=runic_power.deficit<25&death_knight.fwounded_targets>5&!variable.pooling_for_gargoyle
actions.aoe_burst+=/epidemic,if=!death_knight.fwounded_targets&!variable.pooling_for_gargoyle|fight_remains<5|raid_event.adds.exists&raid_event.adds.remains<5
actions.aoe_burst+=/wound_spender
actions.aoe_burst+=/epidemic,if=!variable.pooling_for_gargoyle
actions.aoe_burst+=/call_action_list,name=low_priority

# AoE Setup
actions.aoe_setup=any_dnd,if=death_knight.fwounded_targets>=cycle_enemies|raid_event.adds.exists&raid_event.adds.remains<=11
actions.aoe_setup+=/any_dnd,if=death_knight.fwounded_targets>=5
actions.aoe_setup+=/death_coil,if=buff.dark_transformation.up&runeforge.deadliest_coil&active_enemies<=3
actions.aoe_setup+=/epidemic,if=!variable.pooling_for_gargoyle
actions.aoe_setup+=/festering_strike,cycle_targets=1,if=debuff.festering_wound.stack<=3&cooldown.apocalypse.remains<3
actions.aoe_setup+=/festering_strike,cycle_targets=1,if=debuff.festering_wound.stack<1
actions.aoe_setup+=/festering_strike,cycle_targets=1,if=rune.time_to_4<(cooldown.death_and_decay.remains&!talent.defile|cooldown.defile.remains&talent.defile)
actions.aoe_setup+=/call_action_list,name=low_priority

# Potions and other on use
actions.cooldowns=potion,if=variable.significant_fight&(pet.gargoyle.active|buff.unholy_assault.up|talent.army_of_the_damned&(pet.army_ghoul.active|pet.apoc_ghoul.active|variable.cooldown_army_remains>target.time_to_die))
actions.cooldowns+=/use_item,name=inscrutable_quantum_device,if=(cooldown.unholy_blight.remains|cooldown.dark_transformation.remains)&(pet.army_ghoul.active|pet.apoc_ghoul.active&!talent.army_of_the_damned|target.time_to_pct_20<5)|fight_remains<21
actions.cooldowns+=/use_item,name=macabre_sheet_music,if=cooldown.apocalypse.remains<5&(!equipped.inscrutable_quantum_device|cooldown.inscrutable_quantum_device.remains)|fight_remains<21
actions.cooldowns+=/use_item,name=dreadfire_vessel,if=cooldown.apocalypse.remains&(!equipped.inscrutable_quantum_device|cooldown.inscrutable_quantum_device.remains)|fight_remains<3
actions.cooldowns+=/use_item,name=darkmoon_deck_voracity,if=cooldown.apocalypse.remains&(!equipped.inscrutable_quantum_device|cooldown.inscrutable_quantum_device.remains)|fight_remains<21
actions.cooldowns+=/use_items,if=cooldown.apocalypse.remains&(!equipped.inscrutable_quantum_device|cooldown.inscrutable_quantum_device.remains)
# Cooldowns
actions.cooldowns+=/army_of_the_dead,if=!arena&variable.significant_fight&(cooldown.unholy_blight.remains<3&cooldown.dark_transformation.remains<3&talent.unholy_blight&!soulbind.lead_by_example|!talent.unholy_blight|fight_remains<35)
actions.cooldowns+=/army_of_the_dead,if=!arena&variable.significant_fight&(cooldown.unholy_blight.remains<3&cooldown.abomination_limb.ready&soulbind.lead_by_example)
# Sync Blight with Dark Transformation if utilizing other Dark Transformation buffs, those being Deadliest Coil, Frenzied Monstrosity or Eternal Hunger. Also checks if conditions are met to instead hold for Apocalypse.
actions.cooldowns+=/unholy_blight,if=target.is_enemy&target.in_range&variable.st_planning&(cooldown.dark_transformation.remains<gcd|buff.dark_transformation.up)&(!runeforge.deadliest_coil|!talent.army_of_the_damned|conduit.convocation_of_the_dead.rank<5)
actions.cooldowns+=/unholy_blight,if=active_enemies>=2|fight_remains<21

# Sync Blight with Apocalypse if the cooldown of Apocalypse is low enough. Requires Deadliest Coil, Convocation of the Dead and Army of the Damned together.
actions.cooldowns+=/unholy_blight,if=target.is_enemy&target.in_range&variable.st_planning&(variable.cooldown_army_remains>5|death_knight.disable_aotd)&runeforge.deadliest_coil&talent.army_of_the_damned&conduit.convocation_of_the_dead.rank>=5&cooldown.apocalypse.remains<3&(cooldown.dark_transformation.remains<gcd|buff.dark_transformation.up)
actions.cooldowns+=/unholy_blight,if=target.is_enemy&target.in_range&(active_enemies>=2|fight_remains<21)
actions.cooldowns+=/dark_transformation,if=variable.st_planning&(dot.unholy_blight_dot.remains|!talent.unholy_blight)&target.is_enemy
actions.cooldowns+=/dark_transformation,if=active_enemies>=2&target.is_enemy
actions.cooldowns+=/apocalypse,if=active_enemies=1&debuff.festering_wound.stack>=4&talent.unholy_blight&talent.army_of_the_damned&runeforge.deadliest_coil&conduit.convocation_of_the_dead.rank>=5&dot.unholy_blight_dot.remains
actions.cooldowns+=/apocalypse,if=active_enemies=1&debuff.festering_wound.stack>=4&talent.unholy_blight&dot.unholy_blight_dot.remains&!talent.army_of_the_damned&conduit.convocation_of_the_dead.rank<5
actions.cooldowns+=/apocalypse,if=active_enemies=1&debuff.festering_wound.stack>=4&(!talent.unholy_blight|talent.army_of_the_damned&(!runeforge.deadliest_coil|conduit.convocation_of_the_dead.rank<5)|!talent.army_of_the_damned&conduit.convocation_of_the_dead.rank>=5)
actions.cooldowns+=/apocalypse,cycle_targets=1,if=active_enemies>=2&debuff.festering_wound.stack>=4&!death_and_decay.ticking
actions.cooldowns+=/summon_gargoyle,if=runic_power.deficit<14&(cooldown.unholy_blight.remains<10|dot.unholy_blight_dot.remains)
actions.cooldowns+=/unholy_assault,if=variable.st_planning&debuff.festering_wound.stack<2&(pet.apoc_ghoul.active|conduit.convocation_of_the_dead&buff.dark_transformation.up&!pet.army_ghoul.active)
actions.cooldowns+=/unholy_assault,cycle_targets=1,if=active_enemies>=2&debuff.festering_wound.stack<2
actions.cooldowns+=/soul_reaper,cycle_targets=1,if=target.time_to_pct_35<5&target.time_to_die>5
actions.cooldowns+=/raise_dead,if=!pet.ghoul.active
actions.cooldowns+=/sacrificial_pact,if=active_enemies>=2&!buff.dark_transformation.up&!cooldown.dark_transformation.ready|fight_remains<gcd

# Covenant Abilities
actions.covenants=swarming_mist,if=variable.st_planning&runic_power.deficit>16|fight_remains<11
# Set to use after apoc is on CD as to prevent overcapping RP while setting up CD's
actions.covenants+=/swarming_mist,if=cooldown.apocalypse.remains&(active_enemies>=2&active_enemies<=5&runic_power.deficit>10+(active_enemies*6)|active_enemies>5&runic_power.deficit>40)
actions.covenants+=/abomination_limb,if=variable.st_planning&!soulbind.lead_by_example&cooldown.apocalypse.remains&rune.time_to_4>(3+buff.runic_corruption.remains)|fight_remains<21
actions.covenants+=/abomination_limb,if=variable.st_planning&soulbind.lead_by_example&(dot.unholy_blight_dot.remains>11|!talent.unholy_blight&cooldown.dark_transformation.remains)
actions.covenants+=/abomination_limb,if=active_enemies>=2&rune.time_to_4>(3+buff.runic_corruption.remains)
actions.covenants+=/shackle_the_unworthy,if=variable.st_planning&cooldown.apocalypse.remains|fight_remains<15
actions.covenants+=/shackle_the_unworthy,if=active_enemies>=2&(death_and_decay.ticking|raid_event.adds.remains<=14)

# Single Target
actions.generic=death_coil,if=buff.sudden_doom.react&!variable.pooling_for_gargoyle|pet.gargoyle.active
actions.generic+=/death_coil,if=runic_power.deficit<13&!variable.pooling_for_gargoyle
actions.generic+=/any_dnd,if=cooldown.apocalypse.remains&(talent.defile.enabled|covenant.night_fae|runeforge.phearomones)
actions.generic+=/wound_spender,if=debuff.festering_wound.stack>=4
actions.generic+=/wound_spender,if=debuff.festering_wound.up&cooldown.apocalypse.remains>5&(!talent.unholy_blight|talent.army_of_the_damned&conduit.convocation_of_the_dead.rank<5|!talent.army_of_the_damned&conduit.convocation_of_the_dead.rank>=5|!conduit.convocation_of_the_dead)
actions.generic+=/wound_spender,if=debuff.festering_wound.up&talent.unholy_blight&(!talent.army_of_the_damned&conduit.convocation_of_the_dead.rank<5|talent.army_of_the_damned&conduit.convocation_of_the_dead.rank>=5)&(cooldown.unholy_blight.remains>10&!dot.unholy_blight_dot.remains|cooldown.apocalypse.remains>10)
actions.generic+=/death_coil,if=runic_power.deficit<20&!variable.pooling_for_gargoyle
actions.generic+=/festering_strike,if=debuff.festering_wound.stack<1
actions.generic+=/festering_strike,if=debuff.festering_wound.stack<4&cooldown.apocalypse.remains<5&(!talent.unholy_blight|talent.army_of_the_damned&conduit.convocation_of_the_dead.rank<5|!talent.army_of_the_damned&conduit.convocation_of_the_dead.rank>=5|!conduit.convocation_of_the_dead)
actions.generic+=/festering_strike,if=debuff.festering_wound.stack<4&talent.unholy_blight&(!talent.army_of_the_damned&conduit.convocation_of_the_dead.rank<5|talent.army_of_the_damned&conduit.convocation_of_the_dead.rank>=5)&(cooldown.unholy_blight.remains<10|cooldown.apocalypse.remains<10&dot.unholy_blight_dot.remains)
actions.generic+=/death_coil,if=!variable.pooling_for_gargoyle

actions.generic_aoe=death_coil,if=buff.dark_transformation.up&runeforge.deadliest_coil&active_enemies<=3
# Generic AoE Priority
actions.generic_aoe+=/epidemic,if=buff.sudden_doom.react
actions.generic_aoe+=/epidemic,if=!variable.pooling_for_gargoyle
actions.generic_aoe+=/wound_spender,cycle_targets=1,if=(cooldown.apocalypse.remains>5&debuff.festering_wound.up|debuff.festering_wound.stack>4)&(fight_remains<cooldown.death_and_decay.remains+10|fight_remains>cooldown.apocalypse.remains)
actions.generic_aoe+=/festering_strike,cycle_targets=1,if=debuff.festering_wound.stack<=3&cooldown.apocalypse.remains<3|debuff.festering_wound.stack<1
actions.generic_aoe+=/festering_strike,cycle_targets=1,if=cooldown.apocalypse.remains>5&debuff.festering_wound.stack<1
actions.generic_aoe+=/call_action_list,name=low_priority