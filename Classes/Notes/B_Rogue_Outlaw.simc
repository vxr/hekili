## Outlaw Rogue
## January 25, 2021

## Executed before combat begins. Accepts non-harmful actions only.
# actions.precombat=apply_poison
actions.precombat+=/crippling_poison
actions.precombat+=/instant_poison
actions.precombat+=/bottled_flayedwing_toxin
actions.precombat+=/stealth
actions.precombat+=/marked_for_death,precombat_seconds=5,if=raid_event.adds.in>25&target.exists
actions.precombat+=/roll_the_bones,precombat_seconds=2,if=remains<3&target.exists
actions.precombat+=/slice_and_dice,precombat_seconds=2,if=refreshable&!target.is_player
actions.precombat+=/kidney_shot,precombat_seconds=2,if=target.is_player

actions.sustain_solo+=/crimson_vial,if=health.pct<=80
actions.sustain_solo+=/evasion,if=incoming_physical_3s>=health.pct*0.15
actions.sustain_solo+=/cloak_of_shadows,if=incoming_magic_3s>=health.pct*0.15

actions.sustain_pvp+=/feint,if=incoming_damage_3s>=health.pct*0.10
actions.sustain_pvp+=/crimson_vial,if=health.pct<=80
actions.sustain_pvp+=/evasion,if=incoming_physical_3s>=health.pct*0.15
actions.sustain_pvp+=/cloak_of_shadows,if=incoming_magic_3s>=health.pct*0.15

actions.pvp_interrupts+=/kidney_shot,if=time_to_interrupt<cooldown.kick.remains+gcd.max+0.25&cooldown.kick.remains
actions.pvp_interrupts+=/blind,if=in_pvp&time_to_interrupt<cooldown.kick.remains+gcd.max+0.25&cooldown.kick.remains&(cooldown.kidney_shot.remains|combo_points=0)

## Executed every time the actor is available.
actions+=/use_item,name=sinful_gladiators_medallion
actions+=/kick
actions+=/call_action_list,name=pvp_interrupts,if=target.is_player
# Restealth if possible (no vulnerable enemies in combat)
actions+=/stealth
actions+=/call_action_list,name=sustain_solo,if=solo
actions+=/call_action_list,name=sustain_pvp,if=in_pvp
# Reroll single buffs early other than True Bearing and Broadside
actions+=/variable,name=rtb_reroll,value=rtb_buffs<2&(!buff.true_bearing.up&!buff.broadside.up)
# Ensure we get full Ambush CP gains and aren't rerolling Count the Odds buffs away
actions+=/variable,name=ambush_condition,value=combo_points.deficit>=2+buff.broadside.up&energy>=50&(!conduit.count_the_odds|buff.roll_the_bones.remains>=10)
# Finish at maximum CP but avoid wasting Broadside and Quick Draw bonus combo points
actions+=/variable,name=finish_condition,value=combo_points>=cp_max_spend-buff.broadside.up-(buff.opportunity.up*talent.quick_draw.enabled)|combo_points=animacharged_cp
# With multiple targets, this variable is checked to decide whether some CDs should be synced with Blade Flurry
actions+=/variable,name=blade_flurry_sync,value=spell_targets.blade_flurry<2&raid_event.adds.in>20|buff.blade_flurry.up|fight_remains<=10
actions+=/run_action_list,name=stealth,if=stealthed.all
actions+=/call_action_list,name=cds
actions+=/run_action_list,name=finish,if=variable.finish_condition
actions+=/call_action_list,name=build
actions+=/arcane_torrent,if=energy.deficit>=15+energy.regen
# racials
actions+=/arcane_pulse
actions+=/lights_judgment
actions+=/bag_of_tricks

## Builders
actions.build+=/sepsis
actions.build+=/ghostly_strike
# actions.build+=/shiv,if=runeforge.tiny_toxic_blade
actions.build+=/echoing_reprimand
actions.build+=/serrated_bone_spike,cycle_targets=1,if=!in_pvp&(!dot.serrated_bone_spike.ticking|active_enemies=1&raid_event.adds.in>full_recharge_time|charges>2&fight_remains<5)
# Use Pistol Shot with Opportunity if Combat Potency won't overcap energy, when it will exactly cap CP, or when using Quick Draw
actions.build+=/pistol_shot,if=buff.opportunity.up&(energy.deficit>(energy.regen+10)|combo_points.deficit<=1+buff.broadside.up|talent.quick_draw.enabled)
actions.build+=/pistol_shot,if=buff.opportunity.up&(buff.greenskins_wickers.up|buff.concealed_blunderbuss.up)
actions.build+=/sinister_strike
actions.build+=/gouge,if=talent.dirty_tricks.enabled&combo_points.deficit>=1+buff.broadside.up

## Cooldowns
# Blade Flurry on 2+ enemies
actions.cds=blade_flurry,if=spell_targets>=2&!buff.blade_flurry.up
# Using Ambush is a 2% increase, so Vanish can be sometimes be used as a utility spell unless using Master Assassin or Deathly Shadows
actions.cds+=/vanish,if=!solo&!in_pvp&!runeforge.mark_of_the_master_assassin&!stealthed.all&variable.ambush_condition&(!runeforge.deathly_shadows|buff.deathly_shadows.down&combo_points<=2)
actions.cds+=/vanish,if=!solo&!in_pvp&runeforge.mark_of_the_master_assassin&master_assassin_remains=0&variable.blade_flurry_sync&(!cooldown.between_the_eyes.ready&variable.finish_condition|cooldown.between_the_eyes.ready&variable.ambush_condition)&(!conduit.count_the_odds|buff.roll_the_bones.remains>=10)
actions.cds+=/plunder_armor,if=!stealthed.all
actions.cds+=/flagellation,if=target.time_to_die>=15|target.is_boss
actions.cds+=/flagellation_cleanse,if=debuff.flagellation.remains<2
actions.cds+=/adrenaline_rush,if=!buff.adrenaline_rush.up&(!cooldown.killing_spree.up|!talent.killing_spree.enabled)&((target.time_to_die>=30&time>=5)|(target.is_boss))
actions.cds+=/roll_the_bones,if=master_assassin_remains=0&(buff.roll_the_bones.remains<=3|variable.rtb_reroll)
# If adds are up, snipe the one with lowest TTD. Use when dying faster than CP deficit or without any CP.
actions.cds+=/marked_for_death,cycle_targets=1,if=!in_pvp&active_enemies>1&shortest_ttd<combo_points.deficit&target.time_to_die>=shortest_ttd*1.15&!stealthed.rogue&combo_points.deficit>=cp_max_spend-1
# If no adds will die within the next 30s, use MfD on boss without any CP.
actions.cds+=/marked_for_death,if=!stealthed.rogue&combo_points.deficit>=cp_max_spend-1
actions.cds+=/killing_spree,if=variable.blade_flurry_sync&(energy.time_to_max>2|spell_targets>2)
actions.cds+=/blade_rush,if=variable.blade_flurry_sync&(energy.time_to_max>2|spell_targets>2)
actions.cds+=/dreadblades,if=!stealthed.all&combo_points<=1&(fight_remains>=10|boss)
actions.cds+=/shadowmeld,if=!stealthed.all&variable.ambush_condition
actions.cds+=/potion_of_phantom_fire,if=!solo&!in_pvp&toggle.potions&(buff.bloodlust.react|(target.is_boss&target.time_to_die<=30)|buff.adrenaline_rush.up)&active_enemies<=1
actions.cds+=/potion_of_spectral_agility,if=!solo&!in_pvp&toggle.potions&(buff.bloodlust.react|(target.is_boss&target.time_to_die<=30)|buff.adrenaline_rush.up)&active_enemies>1
# racials
actions.cds+=/blood_fury
actions.cds+=/berserking
actions.cds+=/fireblood
actions.cds+=/ancestral_call
# Default conditions for usable items.
actions.cds+=/use_items,if=!runeforge.mark_of_the_master_assassin&debuff.between_the_eyes.up&(!talent.ghostly_strike.enabled|debuff.ghostly_strike.up)|master_assassin_remains>0|(boss&fight_remains<=20)


actions.finish+=/call_action_list,name=finish_pvp,if=target.is_player
actions.finish+=/call_action_list,name=finish_pve,if=!target.is_player

actions.finish_pvp+=/slice_and_dice,if=buff.slice_and_dice.remains<target.time_to_die&refreshable
actions.finish_pvp+=/kidney_shot
actions.finish_pvp+=/dispatch,if=target.time_to_die<=5
actions.finish_pvp+=/between_the_eyes
actions.finish_pvp+=/dispatch

actions.finish_pve+=/slice_and_dice,if=buff.slice_and_dice.remains<fight_remains&refreshable
# BtE on cooldown to keep the Crit debuff up, unless the target is about to die
actions.finish_pve+=/between_the_eyes,if=target.time_to_die>3
actions.finish_pve+=/dispatch

actions.stealth+=/call_action_list,name=stealth_pvp,if=target.is_player
actions.stealth+=/call_action_list,name=stealth_pve,if=!target.is_player&target.exists

actions.stealth_pvp+=/cheap_shot

actions.stealth_pve+=/dispatch,if=variable.finish_condition
actions.stealth_pve+=/ambush
# actions.stealth_pve+=/cheap_shot,cycle_targets=1,if=talent.prey_on_the_weak.enabled&!target.is_boss
actions.stealth_pve+=/cheap_shot,if=talent.prey_on_the_weak.enabled&!target.is_boss