# Death Knight Unholy
# January 17, 2021

# Adjustments to DnD/Defile/Deaths Due to only count targets who are expected to live long enough to matter (aoe_setup #1).

actions.precombat+=/raise_dead

actions.dead_end=variable,name=dead_end,value=1

# Anti-CC
actions.anti_cc=use_item,name=sinful_gladiators_medallion,if=is_cced
actions.anti_cc+=/icebound_fortitude,interrupt=1,if=is_cced
actions.anti_cc+=/lichborne,if=is_cced&cooldown.icebound_fortitude.remains&buff.icebound_fortitude.down
# Anti-Slow
actions.anti_cc+=/wraith_walk,if=(is_rooted|is_snared)&target.exists&!target.in_range
actions.anti_cc+=/run_action_list,name=dead_end,if=buff.wraith_walk.up&target.exists&!target.in_range
actions.anti_cc+=/deaths_advance,if=(is_rooted|is_snared)&target.exists&!target.in_range&cooldown.wraith_walk.remains&buff.wraith_walk.down

# Sustain
actions.sustain_solo+=/antimagic_shell,if=runic_power.deficit>=35&incoming_magic_5s>=2000
actions.sustain_solo+=/icebound_fortitude,if=health.pct<=45&incoming_damage_5s>=0.2*health.max
actions.sustain_solo+=/lichborne,if=target.is_enemy&target.within7&health.pct<=60&target.time_to_die>=8
actions.sustain_solo+=/antimagic_zone,if=health.pct<=80&incoming_magic_5s>0.25*health.max&target.in_range
actions.sustain_solo+=/death_strike,if=buff.dark_succor.react&health.current<=variable.min_health
actions.sustain_solo+=/death_strike,if=health.current<=variable.min_health&variable.death_strike_overheal=0&variable.death_strike_heal>=variable.death_strike_min_heal
actions.sustain_solo+=/healthstone,if=combat&health.pct<=40&incoming_damage_4s>0.25*health.max
actions.sustain_solo+=/death_strike,if=health.current<incoming_damage_5s
actions.sustain_solo+=/death_strike,if=health.pct<25

# Sustain Group
actions.sustain_group+=/antimagic_shell,if=runic_power.deficit>=35&incoming_magic_5s>=2000
actions.sustain_group+=/icebound_fortitude,if=health.pct<=45&incoming_damage_5s>=0.2*health.max
actions.sustain_group+=/lichborne,if=target.is_enemy&target.within7&health.pct<=60&target.time_to_die>=8
actions.sustain_group+=/death_strike,if=buff.dark_succor.react&health.current<=variable.min_health
actions.sustain_group+=/death_strike,if=health.current<=variable.min_health&variable.death_strike_overheal=0&variable.death_strike_heal>=variable.death_strike_min_heal
actions.sustain_group+=/healthstone,if=combat&health.pct<=40&incoming_damage_4s>0.25*health.max
actions.sustain_group+=/death_strike,if=health.current<incoming_damage_5s
actions.sustain_group+=/death_strike,if=health.pct<25

# Sustain Raid
actions.sustain_raid+=/antimagic_shell,if=runic_power.deficit>=35&incoming_magic_5s>=2000
actions.sustain_raid+=/icebound_fortitude,if=health.pct<=45&incoming_damage_5s>=0.2*health.max
actions.sustain_raid+=/lichborne,if=target.is_enemy&target.within7&health.pct<=60&target.time_to_die>=8
actions.sustain_raid+=/death_strike,if=buff.dark_succor.react&health.current<=variable.min_health
actions.sustain_raid+=/death_strike,if=health.current<=variable.min_health&variable.death_strike_overheal=0&variable.death_strike_heal>=variable.death_strike_min_heal
actions.sustain_raid+=/healthstone,if=combat&health.pct<=40&incoming_damage_4s>0.25*health.max
actions.sustain_raid+=/death_strike,if=health.current<incoming_damage_5s
actions.sustain_raid+=/death_strike,if=health.pct<25

# Executed every time the actor is available.
actions+=/call_action_list,name=anti_cc
actions+=/mind_freeze
actions+=/variable,name=death_strike_heal,value=incoming_damage_5s*0.40
actions+=/variable,name=death_strike_heal,value=health.max*0.105,if=variable.death_strike_heal<health.max*0.105
actions+=/variable,name=min_health,value=health.max*0.895
actions+=/variable,name=min_health,value=health.max*0.80,if=group&!raid
actions+=/variable,name=min_health,value=health.max*0.50,if=raid
actions+=/variable,name=death_strike_overheal,value=(health.current+variable.death_strike_heal)-health.max
actions+=/variable,name=death_strike_overheal,value=0,if=variable.death_strike_overheal<0
actions+=/variable,name=death_strike_min_heal,value=0
actions+=/variable,name=death_strike_min_heal,value=health.max*0.12
actions+=/variable,name=death_strike_min_heal,value=health.max*0.16
actions+=/call_action_list,name=sustain_solo,if=solo
actions+=/call_action_list,name=sustain_raid,if=raid
actions+=/call_action_list,name=sustain_group,if=group&!raid
# Variables
actions+=/variable,name=pooling_runic_power,value=cooldown.summon_gargoyle.remains<5&talent.summon_gargoyle
actions+=/variable,name=pooling_runes,value=talent.soul_reaper&rune<2&target.time_to_pct_35<5&fight_remains>5
actions+=/variable,name=st_planning,value=active_enemies=1&(!raid_event.adds.exists|raid_event.adds.in>15)
actions+=/variable,name=major_cooldowns_active,value=pet.gargoyle.active|buff.unholy_assault.up|talent.army_of_the_damned&pet.apoc_ghoul.active|buff.dark_transformation.up
# Racials
actions+=/arcane_torrent,if=runic_power.deficit>65&(pet.gargoyle.active|!talent.summon_gargoyle.enabled)&rune.deficit>=5
# Maintaining Virulent Plague is a priority
actions+=/outbreak,if=dot.virulent_plague.refreshable&!talent.unholy_blight&active_enemies=1
actions+=/outbreak,if=dot.virulent_plague.refreshable&active_enemies>=2&(!talent.unholy_blight|talent.unholy_blight&cooldown.unholy_blight.remains)
actions+=/outbreak,if=runeforge.superstrain&(dot.frost_fever.refreshable|dot.blood_plague.refreshable)
# Action Lists and Openers (Openers/Sequences unsupported in addon.)
actions+=/call_action_list,name=trinkets
actions+=/call_action_list,name=covenants
actions+=/call_action_list,name=cooldowns
actions+=/call_action_list,name=aoe_setup,if=active_enemies>=2&(cooldown.death_and_decay.remains<10&!talent.defile|cooldown.defile.remains<10&talent.defile)&!death_and_decay.ticking
actions+=/call_action_list,name=aoe_burst,if=active_enemies>=2&death_and_decay.ticking
actions+=/call_action_list,name=generic_aoe,if=active_enemies>=2&!death_and_decay.ticking
actions+=/call_action_list,name=generic

# AoE Burst
actions.aoe_burst=death_coil,if=(buff.sudden_doom.react|!variable.pooling_runic_power)&(buff.dark_transformation.up&runeforge.deadliest_coil&active_enemies<=3|active_enemies=2)
actions.aoe_burst+=/epidemic,if=runic_power.deficit<(10+death_knight.fwounded_targets*3)&death_knight.fwounded_targets<6&!variable.pooling_runic_power|buff.swarming_mist.up
actions.aoe_burst+=/epidemic,if=runic_power.deficit<25&death_knight.fwounded_targets>5&!variable.pooling_runic_power
actions.aoe_burst+=/epidemic,if=!death_knight.fwounded_targets&!variable.pooling_runic_power|fight_remains<5|active_enemies>=2&fight_remains<5
actions.aoe_burst+=/wound_spender
actions.aoe_burst+=/epidemic,if=!variable.pooling_runic_power

# AoE Setup
actions.aoe_setup+=/any_dnd,if=death_knight.fwounded_targets>=2&active_enemies>=3&ttds_after_10>=2
actions.aoe_setup+=/death_coil,if=!variable.pooling_runic_power&(buff.dark_transformation.up&runeforge.deadliest_coil&active_enemies<=3|active_enemies=2)
actions.aoe_setup+=/epidemic,if=!variable.pooling_runic_power
actions.aoe_setup+=/festering_strike,if=debuff.festering_wound.stack<=3&cooldown.apocalypse.remains<3
actions.aoe_setup+=/festering_strike,if=debuff.festering_wound.stack<1
actions.aoe_setup+=/festering_strike,if=rune.time_to_4<(cooldown.death_and_decay.remains&!talent.defile|cooldown.defile.remains&talent.defile)

# Potion
actions.cooldowns=potion,if=!solo&(variable.major_cooldowns_active|(target.is_boss&target.time_to_die<=25))
# Cooldowns
actions.cooldowns+=/army_of_the_dead,if=talent.unholy_blight&cooldown.unholy_blight.remains<5&cooldown.dark_transformation.remains<5&talent.unholy_blight&ttds_after_40>=1&time>3
actions.cooldowns+=/army_of_the_dead,if=!talent.unholy_blight&ttds_after_40>=1&time>3
actions.cooldowns+=/soul_reaper,target_if=target.time_to_pct_35<5&target.time_to_die>5
actions.cooldowns+=/unholy_blight,if=pet.army_ghoul.active&target.in_range
actions.cooldowns+=/unholy_blight,if=target.in_range&(ttds_after_18>=2|(target.is_boss&target.time_to_die<=21))
actions.cooldowns+=/dark_transformation,if=pet.army_ghoul.active
actions.cooldowns+=/dark_transformation,if=ttds_after_20>=2|(target.is_boss&target.time_to_die<=24)
actions.cooldowns+=/apocalypse,if=active_enemies=1&target.time_to_die>=15&debuff.festering_wound.stack>=4&talent.unholy_blight&talent.army_of_the_damned&runeforge.deadliest_coil&conduit.convocation_of_the_dead.rank>=5&dot.unholy_blight_dot.remains
actions.cooldowns+=/apocalypse,if=active_enemies=1&target.time_to_die>=15&debuff.festering_wound.stack>=4&talent.unholy_blight&dot.unholy_blight_dot.remains&!talent.army_of_the_damned&conduit.convocation_of_the_dead.rank<5
actions.cooldowns+=/apocalypse,if=active_enemies=1&target.time_to_die>=15&debuff.festering_wound.stack>=4&(!talent.unholy_blight|talent.army_of_the_damned&(!runeforge.deadliest_coil|conduit.convocation_of_the_dead.rank<5)|!talent.army_of_the_damned&conduit.convocation_of_the_dead.rank>=5|fight_remains<16)
actions.cooldowns+=/apocalypse,if=ttds_after_20>=2&debuff.festering_wound.stack>=4
actions.cooldowns+=/summon_gargoyle,if=runic_power.deficit<14&(cooldown.unholy_blight.remains<10|dot.unholy_blight_dot.remains)
actions.cooldowns+=/unholy_assault,if=ttds_after_15>=1&debuff.festering_wound.stack<=2
actions.cooldowns+=/raise_dead,if=!pet.ghoul.active
actions.cooldowns+=/sacrificial_pact,if=active_enemies>=2&!buff.dark_transformation.up&!cooldown.dark_transformation.ready&cooldown.raise_dead.ready

# Covenant Abilities
actions.covenants+=/abomination_limb,if=active_enemies>=2&rune.time_to_4>(3+buff.runic_corruption.remains)

# Single Target
actions.generic=death_coil,if=buff.sudden_doom.react&!variable.pooling_runic_power|pet.gargoyle.active
actions.generic+=/death_coil,if=runic_power.deficit<13
actions.generic+=/any_dnd,if=cooldown.apocalypse.remains&(talent.defile.enabled|covenant.night_fae|runeforge.phearomones)&!variable.pooling_runes
actions.generic+=/wound_spender,if=debuff.festering_wound.stack>4&!variable.pooling_runes
actions.generic+=/wound_spender,if=debuff.festering_wound.up&cooldown.apocalypse.remains>5&!variable.pooling_runes&(!talent.unholy_blight|talent.army_of_the_damned&conduit.convocation_of_the_dead.rank<5|!talent.army_of_the_damned&conduit.convocation_of_the_dead.rank>=5|!conduit.convocation_of_the_dead)
actions.generic+=/wound_spender,if=debuff.festering_wound.up&talent.unholy_blight&!variable.pooling_runes&(!talent.army_of_the_damned&conduit.convocation_of_the_dead.rank<5|talent.army_of_the_damned&conduit.convocation_of_the_dead.rank>=5)&(cooldown.unholy_blight.remains>10&!dot.unholy_blight_dot.remains|cooldown.apocalypse.remains>10)
actions.generic+=/death_coil,if=runic_power.deficit<20&!variable.pooling_runic_power
actions.generic+=/festering_strike,if=debuff.festering_wound.stack<1&!variable.pooling_runes
actions.generic+=/festering_strike,if=debuff.festering_wound.stack<4&cooldown.apocalypse.remains<5&!variable.pooling_runes&(!talent.unholy_blight|talent.army_of_the_damned&conduit.convocation_of_the_dead.rank<5|!talent.army_of_the_damned&conduit.convocation_of_the_dead.rank>=5|!conduit.convocation_of_the_dead)
actions.generic+=/festering_strike,if=debuff.festering_wound.stack<4&talent.unholy_blight&!variable.pooling_runes&(!talent.army_of_the_damned&conduit.convocation_of_the_dead.rank<5|talent.army_of_the_damned&conduit.convocation_of_the_dead.rank>=5)&(cooldown.unholy_blight.remains<10|cooldown.apocalypse.remains<10&dot.unholy_blight_dot.remains)
actions.generic+=/death_coil,if=!variable.pooling_runic_power
actions.generic+=/festering_strike,if=rune.deficit=0&runic_power.deficit>=20&debuff.festering_wound.stack<=4
actions.generic+=/wound_spender,if=rune.deficit=0&runic_power.deficit>=10&debuff.festering_wound.stack>=5

# Generic AoE Priority
actions.generic_aoe+=/death_coil,if=(!variable.pooling_runic_power|buff.sudden_doom.react)&(buff.dark_transformation.up&runeforge.deadliest_coil&active_enemies<=3|active_enemies=2)
actions.generic_aoe+=/epidemic,if=buff.sudden_doom.react|!variable.pooling_runic_power
actions.generic_aoe+=/wound_spender,if=(cooldown.apocalypse.remains>5&debuff.festering_wound.up|debuff.festering_wound.stack>4)&(fight_remains<cooldown.death_and_decay.remains+10|fight_remains>cooldown.apocalypse.remains)
actions.generic_aoe+=/festering_strike,if=debuff.festering_wound.stack<=3&cooldown.apocalypse.remains<3|debuff.festering_wound.stack<1
actions.generic_aoe+=/festering_strike,if=cooldown.apocalypse.remains>5&debuff.festering_wound.stack<1

# Trinkets
actions.trinkets+=/use_item,name=inscrutable_quantum_device,if=variable.major_cooldowns_active|(boss&fight_remains<=20)
actions.trinkets+=/use_item,name=darkmoon_deck_voracity,if=variable.major_cooldowns_active|(boss&fight_remains<=20)